plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'info.solidsoft.pitest' version '1.7.0'
}

group 'com.montealegreluis'

apply from: rootProject.file('hooks.gradle')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/api-problem")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/activity-feed")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/montealegreluis/api-problem-spring-boot")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    api 'org.springframework:spring-web:5.3.16'
    api 'javax.validation:validation-api:2.0.1.Final'
    api "com.montealegreluis:activity-feed:${activityFeedVersion}"
    api "com.montealegreluis:api-problem:${apiProblemVersion}"
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'net.logstash.logback:logstash-logback-encoder:7.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

jacocoTestReport {
    dependsOn test
    reports {
        html.destination file("${buildDir}/coverage")
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.15'
    coverageThreshold = 98
    mutationThreshold = 98
}

